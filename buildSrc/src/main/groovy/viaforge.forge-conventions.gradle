plugins {
    id "net.minecraftforge.gradle"
    id "org.spongepowered.mixin"

    id "viaforge.shadow-conventions"
}

minecraft {
    runs {
        client {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"

            // mixin
            property "mixin.debug.export", "true"
            property "mixin.hotSwap", "true"
            property "fml.coreMods.load", "de.florianmichael.viaforge.mixin.MixinLoader" // Only required for MC 1.12, but modern Forges skips this anyway
            args "-mixin.config=" + "mixins." + project.getProperty('maven_name') + ".json"

            // source set
            mods {
                "ViaForge" {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir "src/generated/resources"
}

dependencies {
    include "org.spongepowered:mixin:${mixin_version}"
    include "org.slf4j:slf4j-api:${slf4j_version}"

    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    include project(":") // Include the base project, to get Common-ViaForge
}

mixin {
    add sourceSets.main, "mixins.${maven_name}.refmap.json"
}

jar {
    manifest.attributes(
            "Specification-Title": "viaforge",
            "Specification-Vendor": "viaforge",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${archiveVersion}",
            "Implementation-Vendor" :"viaforge",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd"-"HH:mm:ssZ"),
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
            "FMLCorePluginContainsFMLMod": "true", // Only required for MC 1.12, but modern Forges skips this anyway
            "FMLCorePlugin": "de.florianmichael.viaforge.mixin.MixinLoader", // Counterpart to the above
            "MixinConfigs": "mixins.${maven_name}.json",
            "ForceLoadAsMod": "true"
    )
    enabled = false
}

shadowJar {
    archiveFileName = jar.archiveFileName
    configurations = [project.configurations.include] // Include the dependencies from the include configuration
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    // Prevent conflicts with Forge's weird service loading
    exclude("META-INF/maven/**")
    exclude("META-INF/versions/**")
}

reobf {
    shadowJar {}
}

jar.dependsOn("shadowJar")
